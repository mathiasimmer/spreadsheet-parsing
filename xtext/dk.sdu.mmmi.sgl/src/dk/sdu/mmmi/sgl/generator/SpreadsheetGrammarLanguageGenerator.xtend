/*
 * generated by Xtext
 */
package dk.sdu.mmmi.sgl.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IGenerator
import org.eclipse.xtext.generator.IFileSystemAccess
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.Grammar
import java.util.List
import java.util.ArrayList
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.Block
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.Rule
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.Column
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.ColumnDefinition
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.RowSpec
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.ColumnSpec
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.BlockSpec
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.MandatoryColumn
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.OptionalColumn
import dk.sdu.mmmi.sgl.spreadsheetGrammarLanguage.Syntax

/**
 * Generates code from your model files on save.
 * 
 * see http://www.eclipse.org/Xtext/documentation.html#TutorialCodeGeneration
 */
class SpreadsheetGrammarLanguageGenerator implements IGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		resource.allContents.filter(typeof(Grammar)).forEach[compile(fsa)]
//		fsa.generateFile('greetings.txt', 'People to greet: ' + 
//			resource.allContents
//				.filter(typeof(Greeting))
//				.map[name]
//				.join(', '))
	}
	
	def compile(Grammar grammar, IFileSystemAccess fsa) {
		fsa.generateFile("generated/Parser_"+grammar.name+".py", grammar.generate)
	}
	
	def generate(Grammar grammar) '''
	from spreadsheet_parser import *
	class Parse«grammar.name»(GenericParserHelper):
		def __init__(self, spreadsheet):
			super(GenericParserHelper,self).__init__(spreadsheet)
		def matchColumns(self,columnHeaders):
			return columnHeaders==[«FOR h:grammar.computeHeaders SEPARATOR ","»"«h»"«ENDFOR»]
		def parseBlock(self,columnHeaders,row,column,height):
			results = []
			relativeRow = 0
			while relativeRow<height:
				increment_and_object = self.parse_«grammar.root.name»(row+relativeRow,column)
				results.append(increment_and_object[1])
				relativeRow += increment_and_object[0]
			return results
		«FOR e:grammar.elements»
		«e.genParser»
		«ENDFOR»
	'''
	
	//
	// Parse functions for grammar
	//
	
	def dispatch genParser(Rule rule) '''
	def parse_syntax_«rule.name»(text):
		return text
	'''

	def dispatch genParser(Block block) '''
	def parse_«block.name»(self,row,column):
		column_offset = 0
		result_row_increment = 1
		result_object = {}
		«FOR c:block.columns»
		# Column «c.name»
		current_column = column+column_offset
		«IF c.multiple»
		«c.def.genParserMultiple(c.name)»
		«ELSE»
		«c.def.genParserSingle(c.name)»
		«ENDIF»
		result_object["«c.name»"] = value_«c.name»
		column_offset += 1
		«ENDFOR»
		return (result_row_increment,result_object)
	'''
	
	def dispatch genParserMultiple(MandatoryColumn col, String name) '''
	value_«name» = []
	«col.spec.genParserMultipleBody(name)»
	'''
	
	def dispatch genParserMultiple(OptionalColumn col, String name) '''
	value_«name» = []
	if not self.emptyCell(row,current_column):
		«col.spec.genParserMultipleBody(name)»
	'''

	def dispatch genParserSingle(MandatoryColumn col, String name) '''
	value_«name» = «col.spec.genParserSingleBody»
	'''
	
	def dispatch genParserSingle(OptionalColumn col, String name) '''
	if self.emptyCell(self,row,current_column):
		value_«name» = None
	else:
		value_«name» = «col.spec.genParserSingleBody»
	'''

	def dispatch genParserSingleBody(RowSpec spec) '''
	parse_syntax_«spec.syntax.generateSyntaxName»(self.getCell(row,current_column))
	'''
	
	def dispatch genParserSingleBody(BlockSpec spec) {
		throw new Error("Illegal grammar: block with single-relation column")
	}

	def dispatch genParserMultipleBody(RowSpec spec, String name) '''
	relativeRow = 0
	while True:
		value_«name».append(self.parse_syntax_«spec.syntax.generateSyntaxName»(self.getCell(row+relativeRow,current_column+1))
		relativeRow += 1
		result_row_increment += 1
		if not self.emptyCell(row+relativeRow,current_column):
			break
	'''

	def dispatch genParserMultipleBody(BlockSpec spec, String name) '''
	relativeRow = 0
	while True:
		increment_and_object = self.parse_«spec.kind.name»(row+relativeRow,current_column+1)
		relativeRow += increment_and_object[0]
		result_row_increment += increment_and_object[0]
		value_«name».append(increment_and_object[1])
		if not self.emptyCell(row+relativeRow,current_column):
			break
	'''

	// Helper stuff
	
	def String generateSyntaxName(Syntax syntax) {
		if(syntax.is_id) "IDENTIFIER"
		else if(syntax.is_string) "STRING"
		else if(syntax.is_int) "INTEGER"
		else if(syntax.token!=null) "token(\""+syntax.token+"\")"
		else syntax.rule.name
	}
	
	//
	// Computation of headers
	//
	
	def List<String> computeHeaders(Grammar grammar) {
		val result = new ArrayList<String>()
		grammar.root.collectHeaders(result)
		result
	}
	
	def dispatch void collectHeaders(Block block, List<String> collector) {
		block.columns.forEach[collectHeaders(collector)]
	}
	
	def dispatch void collectHeaders(Rule rule, List<String> collector) { }
	
	def dispatch void collectHeaders(Column column, List<String> collector) {
		column.def.collectHeaders(collector)
	}
	
	def dispatch void collectHeaders(ColumnDefinition cdef, List<String> collector) {
		cdef.spec.collectHeaders(collector)
	}

	def dispatch void collectHeaders(RowSpec spec, List<String> collector) {
		collector.add(spec.header)
	}

	def dispatch void collectHeaders(BlockSpec spec, List<String> collector) {
		spec.kind.collectHeaders(collector)
	}

}
